pluginManagement {
    plugins {
        id 'com.intershop.gradle.javacc' version '4.0.1'
    }
    repositories {
        gradlePluginPortal()
        mavenCentral()
    }
}

plugins {
    id "com.gradle.develocity" version "3.17.6"
    id "io.spring.ge.conventions" version "0.0.17"
}

dependencyResolutionManagement {
    repositories {
        mavenCentral()
        maven { url "https://repo.spring.io/snapshot" }
    }
}

rootProject.name = 'spring-ldap'

include 'core'
include 'dependencies'
include 'test-support'
include 'ldif/ldif-core'
include 'odm'
include 'sandbox'
include 'test/integration-tests'
//include 'test/integration-tests-spring20'
//include 'test/integration-tests-spring25'
//include 'test/integration-tests-spring30'
include 'test/integration-tests-openldap'
include 'test/integration-tests-sunone'
include 'test/integration-tests-ad'
include 'samples/plain'
include 'samples/odm'
include 'samples/boot'


rootProject.children.each { p->
    def name = p.name
    def isSample = name.contains("sample")
    name = name.replaceFirst(".*?/","")
    if(isSample && !name.contains("sample")) {
        name += "-sample"
    }
    p.name = "spring-ldap-" + name
}

findProject(":spring-ldap-test-support").name = "spring-ldap-test"
