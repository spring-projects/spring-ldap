plugins {
    id 'io.spring.convention.spring-module'
    id 'com.intershop.gradle.javacc'
}

javacc {
    // configuration container for all javacc configurations
    configs {
        template {
            inputFile = file('src/main/javacc/DnParserImpl.jj')
        }
    }
}

dependencies {
    management platform(project(":spring-ldap-dependencies"))
    api "org.springframework:spring-core"
    api "org.springframework:spring-beans"
    api "org.springframework:spring-context"
    api "org.springframework:spring-tx"
    api "io.micrometer:micrometer-core"

    implementation "org.slf4j:slf4j-api"

    provided "com.sun:ldapbp:1.0"

    optional "org.springframework:spring-context"
    optional "org.springframework:spring-jdbc"
    optional "org.springframework:spring-orm"
    optional "org.springframework.data:spring-data-commons"
    optional "com.querydsl:querydsl-core"
    optional "com.querydsl:querydsl-apt"
    optional "commons-pool:commons-pool"
    optional "org.apache.commons:commons-pool2"

    testImplementation ("io.micrometer:micrometer-test") {
        exclude group: 'org.mockito'
        exclude group: 'org.assertj'
    }
    testImplementation platform('org.junit:junit-bom')
    testImplementation "org.junit.vintage:junit-vintage-engine"

    testImplementation "junit:junit"
    testImplementation "commons-lang:commons-lang"
    testImplementation "gsbase:gsbase"
    testImplementation "org.mockito:mockito-core"
    testImplementation "org.springframework:spring-test"
    testImplementation "org.assertj:assertj-core"
    testImplementation "com.unboundid:unboundid-ldapsdk"
}

compileTestJava {
    doFirst {
        options.compilerArgs = [
                '--add-exports', 'java.naming/com.sun.jndi.ldap=ALL-UNNAMED'
        ]
    }
}

test {
    jvmArgs '--add-exports', 'java.naming/com.sun.jndi.ldap=ALL-UNNAMED'
}

checkFormatMain {
    enabled = false
}
