buildscript {
    repositories {
        maven { url 'https://repo.spring.io/plugins-release' }
    }
    dependencies {
        classpath("org.springframework.build.gradle:propdeps-plugin:0.0.7")
        classpath 'org.asciidoctor:asciidoctorj-pdf:1.5.0-alpha.16'
        classpath("io.spring.gradle:spring-io-plugin:0.0.6.RELEASE")
    }
}

plugins {
    id "org.sonarqube" version "2.1-rc1"
    id 'org.asciidoctor.convert' version '1.5.10'
}

repositories {
	maven { url 'https://repo.spring.io/plugins-release' }
	mavenCentral()
}

apply plugin: 'org.asciidoctor.gradle.asciidoctor'

ext.docResourcesVersion = '0.2.1.RELEASE'
ext.GRADLE_SCRIPT_DIR = "${rootProject.projectDir}/gradle"
ext.JAVA_MODULE_SCRIPT = "${GRADLE_SCRIPT_DIR}/java-module.gradle"
ext.MAVEN_DEPLOYMENT_SCRIPT = "${GRADLE_SCRIPT_DIR}/maven-deployment.gradle"
ext.JAVA_SCRIPT = "${GRADLE_SCRIPT_DIR}/java.gradle"
ext.RELEASE_CHECKS_SCRIPT = "${GRADLE_SCRIPT_DIR}/release-checks.gradle"
ext.SAMPLE_WAR_GRADLE = "${GRADLE_SCRIPT_DIR}/sample-war.gradle"
ext.SPRING_SNAPSHOT_TEST_SCRIPT = "${GRADLE_SCRIPT_DIR}/spring-snapshot-test.gradle"

ext.coreModules = subprojects.findAll { p-> (!p.name.equals('spring-build-src') && !p.name.contains("test") && !p.name.contains("sample") && !p.name.contains("sandbox")) || p.name.contains("spring-ldap-test") }

allprojects {
    apply plugin: 'idea'
    apply plugin: 'eclipse'
    apply plugin: 'java'

    group = "org.springframework.ldap"

    ext.releaseBuild = version.endsWith('RELEASE')
    ext.snapshotBuild = version.endsWith('SNAPSHOT')

    ext.javadocLinks = [
        "https://docs.oracle.com/javase/7/docs/api/",
        "https://docs.spring.io/spring/docs/3.2.x/javadoc-api/",
        "https://logging.apache.org/log4j/1.2/apidocs/",
        "https://commons.apache.org/proper/commons-logging/apidocs/",
        "https://commons.apache.org/proper/commons-dbcp/apidocs/",
        "https://commons.apache.org/proper/commons-pool/apidocs/",
        "http://junit.sourceforge.net/javadoc/",
    ] as String[]
}


configure(subprojects) {
    apply plugin: 'propdeps'
    apply plugin: 'propdeps-maven'
    apply plugin: 'propdeps-idea'
    apply plugin: 'propdeps-eclipse'
    apply plugin: 'groovy'
    apply plugin: "merge"

}

configure(coreModules) {
    apply from: JAVA_MODULE_SCRIPT
}

configure(subprojects - coreModules) {
    sonarqube {
        skipProject = true
    }

    tasks.findByPath("artifactoryPublish")?.enabled = false
}

asciidoctorj {
	version = '1.5.5'
}

configurations {
	docs
}

dependencies {
	docs "io.spring.docresources:spring-doc-resources:${docResourcesVersion}@zip"
}

task prepareAsciidocBuild(type: Sync) {
	dependsOn configurations.docs
	// copy doc resources
	from {
		configurations.docs.collect { zipTree(it) }
	}
	// and doc sources
	from "src/docs/asciidoc/"
	// to a build directory of your choice
	into "$buildDir/asciidoc/assemble"
}

description = "Spring LDAP"

configurations.archives.artifacts.clear()

sonarqube {
    properties {
        property "sonar.exclusions", "file:**/generated-src/**"
        property "sonar.java.coveragePlugin", "jacoco"
        property "sonar.jacoco.reportPath", "${buildDir.name}/jacoco.exec"
        property "sonar.links.homepage", 'https://github.com/SpringSource/spring-ldap'
        property "sonar.links.ci", 'https://build.springsource.org/browse/LDAP-B20X'
        property "sonar.links.issue", 'https://jira.springsource.org/browse/LDAP'
        property "sonar.links.scm", 'https://github.com/SpringSource/spring-ldap'
        property "sonar.links.scm_dev", 'https://github.com/SpringSource/spring-ldap.git'
    }
}

task('makePDF', type: org.asciidoctor.gradle.AsciidoctorTask){
dependsOn prepareAsciidocBuild
  backends 'pdf'
  sourceDir "$buildDir/asciidoc/assemble"
  outputDir = new File("$buildDir/docs")
	sources {
		include 'index.adoc'
	}
	options doctype: 'book', eruby: 'erubis'
	logDocuments = true
	attributes 'docinfo': 'shared',
		'icons': 'font',
    'sectanchors': '',
    'source-highlighter' : 'coderay',
    'spring-ldap-version' : project.version,
    revnumber : project.version
}

asciidoctor {
	dependsOn makePDF
	backends 'html5'
	sourceDir "$buildDir/asciidoc/assemble"
  outputDir = new File("$buildDir/docs")
	sources {
		include 'index.adoc'
	}
	resources {
		from(sourceDir) {
			include 'images/*', 'css/**', 'js/**'
		}
	}
	options doctype: 'book', eruby: 'erubis'
	logDocuments = true
	attributes 'docinfo': 'shared',
		// use provided stylesheet
		stylesdir: "css/",
		stylesheet: 'spring.css',
		'linkcss': true,
		'icons': 'font',
    'sectanchors': '',
		// use provided highlighter
		'source-highlighter=highlight.js',
		'highlightjsdir=js/highlight',
		'highlightjs-theme=github',
    'spring-ldap-version' : project.version,
    revnumber : project.version
}

task api(type: Javadoc) {
    group = "Documentation"
    description = "Generates aggregated Javadoc API documentation."
    title = "${rootProject.description} ${version} API"

    options.memberLevel = org.gradle.external.javadoc.JavadocMemberLevel.PROTECTED
    options.author = true
    options.header = rootProject.description
    options.splitIndex = true
    options.links(project.ext.javadocLinks)

    maxMemory = "1536m"
    destinationDir = new File(buildDir, "api")

    source coreModules*.javadoc*.source
    classpath = files(coreModules*.javadoc*.classpath)
}

task docsZip(type: Zip, dependsOn: asciidoctor) {
    group = "Distribution"
    baseName = "spring-ldap"
    classifier = "docs"
    description = "Builds -${classifier} archive containing api and reference " +
        "for deployment at https://docs.spring.io/spring-ldap/docs."

    from("src/dist") {
        include "changelog.txt"
    }

    from (api) {
        into "apidocs"
    }

    from (new File(asciidoctor.outputDir, "html5")) {
        include "*.html"
        include 'images/*', 'css/**', 'js/**'
        into "reference"
    }
    from (new File(asciidoctor.outputDir, "pdf")) {
        include "*.pdf"
        into "reference"
    }
}

task schemaZip(type: Zip) {
    group = 'Distribution'
    baseName = rootProject.name
    classifier = 'schema'
    description = "Builds -${classifier} archive containing all " +
        "XSDs for deployment at static.springframework.org/schema."

    coreModules.each { module ->
        def Properties schemas = new Properties();

        module.sourceSets.main.resources.find {
            it.path.endsWith('META-INF/spring.schemas')
        }?.withInputStream { schemas.load(it) }

        for (def key : schemas.keySet()) {
            def shortName = key.replaceAll(/http.*schema.(.*).spring-.*/, '$1')
            assert shortName != key
            File xsdFile = module.sourceSets.main.resources.find {
                it.path.endsWith(schemas.get(key))
            }
            assert xsdFile != null
            into (shortName) {
                from xsdFile.path
            }
        }
    }
}


task distZip(type: Zip, dependsOn: [docsZip]) {
    dependsOn subprojects*.tasks*.matching { task -> task.name == 'assemble' }

    group = "Distribution"
    baseName = "spring-ldap"
    classifier = "dist"
    description = "Builds -${classifier} archive, containing all jars and docs, " +
                "suitable for community download page."

    ext.baseDir = "${baseName}-${project.version}"


    from("src/dist") {
        include "readme.md"
        include "license.txt"
        include "notice.txt"
        into "${baseDir}"
        expand(copyright: new Date().format("yyyy"), version: project.version)
    }

    from(zipTree(docsZip.archivePath)) {
        into "${baseDir}/docs"
    }

    coreModules.each { subproject ->
        into ("${baseDir}/libs") {
            from subproject.jar
            if (subproject.tasks.findByPath("sourcesJar")) {
                from subproject.sourcesJar
            }
            if (subproject.tasks.findByPath("javadocJar")) {
                from subproject.javadocJar
            }
        }
    }
}

artifacts {
    archives docsZip
    archives distZip
    archives schemaZip
}
