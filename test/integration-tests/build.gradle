apply plugin: 'io.spring.convention.spring-test'

idea.module.excludeDirs = [
        file('.gradle'),
        file('build/classes'),
        file('build/tmp'),
        file('build/dependency-cache'),
        file('build/libs')]

sourceSets {
    generated {
        java {
            srcDirs = ['build/generated-src']
        }
    }
}

dependencies {
    management platform(project(":spring-ldap-dependencies"))
    implementation 	project(":spring-ldap-test"),
                project(":spring-ldap-core-tiger")

    implementation "com.querydsl:querydsl-apt",
            "commons-lang:commons-lang"

    implementation("org.springframework.data:spring-data-commons") {
        exclude group: "org.springframework", module: "spring-beans"
        exclude group: "org.springframework", module: "spring-core"
    }

    implementation("org.springframework.security:spring-security-core") {
        exclude group: "org.springframework", module: "spring-expression"
        exclude group: "org.springframework", module: "spring-core"
        exclude group: "org.springframework", module: "spring-context"
        exclude group: "org.springframework", module: "spring-tx"
        exclude group: "org.springframework", module: "spring-aop"
    }

    provided "commons-pool:commons-pool"
    provided "org.springframework:spring-jdbc"
    provided "org.springframework:spring-orm"

    optional "org.apache.directory.server:apacheds-core-entry"
    optional "org.apache.directory.server:apacheds-core"
    optional "org.apache.directory.server:apacheds-protocol-ldap"
    optional "org.apache.directory.server:apacheds-protocol-shared"
    optional "org.apache.directory.server:apacheds-server-jndi"
    optional "org.apache.directory.shared:shared-ldap"
    optional "com.unboundid:unboundid-ldapsdk"

    testImplementation "org.springframework:spring-test"
    testImplementation "org.springframework:spring-aop"
    testImplementation "org.springframework:spring-expression"
    testImplementation "org.hibernate:hibernate-core"
    testImplementation "org.aspectj:aspectjrt"
    testImplementation "org.aspectj:aspectjweaver"
    testImplementation "org.hsqldb:hsqldb"
    testImplementation "junit:junit"
    testImplementation "org.springframework.security:spring-security-config"
    testImplementation "org.slf4j:slf4j-log4j12"
    testImplementation "org.assertj:assertj-core"

    testImplementation platform('org.junit:junit-bom')
    testImplementation "org.junit.vintage:junit-vintage-engine"

    testImplementation("org.springframework.security:spring-security-ldap") {
        exclude group: "org.springframework.ldap", module: "spring-ldap-core"
    }
}

compileGeneratedJava {
    options.warnings = false
    classpath += sourceSets.main.runtimeClasspath
}

clean {
    delete sourceSets.generated.java.srcDirs
}

idea {
    module {
        sourceDirs += file('build/generated-src')
    }
}

compileTestJava {
    targetCompatibility = JavaVersion.VERSION_11
}

test {
    systemProperties = System.properties
}

